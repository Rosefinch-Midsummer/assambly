# 8-2参数传递

<!-- toc -->

<a href="https://rosefinch-midsummer.github.io/book/file/as/8-2.pdf" target="_blank">在线阅读PDF文档</a>


![](https://cdn.jsdelivr.net/gh/Rosefinch-Midsummer/MyImagesHost02/img/202310250957866.png)



## 寄存器传递参数

![](https://cdn.jsdelivr.net/gh/Rosefinch-Midsummer/MyImagesHost02/img/202310251002762.png)

![](https://cdn.jsdelivr.net/gh/Rosefinch-Midsummer/MyImagesHost02/img/202310251010363.png)

![](https://cdn.jsdelivr.net/gh/Rosefinch-Midsummer/MyImagesHost02/img/202310251016515.png)

![](https://cdn.jsdelivr.net/gh/Rosefinch-Midsummer/MyImagesHost02/img/202310251020233.png)

![](https://cdn.jsdelivr.net/gh/Rosefinch-Midsummer/MyImagesHost02/img/202310251023862.png)

![](https://cdn.jsdelivr.net/gh/Rosefinch-Midsummer/MyImagesHost02/img/202310251027347.png)

![](https://cdn.jsdelivr.net/gh/Rosefinch-Midsummer/MyImagesHost02/img/202310251031922.png)


一个双字（二进制32位）数据，以十六进制形式显示，需要显示十六进制8位。

4位二进制代码“1010”对应十六进制“A”，将这个4位二进制代码按照十六进制输出，需要转换为字母“A”的ASCII，这需要在这个4位二进制代码基础上加：37H

对数值0～9，只要加30H，就依次成为字符'0'～'9'的ASCII码。

```asm
; eg0503.asm in Windows Console
	include io32.inc
	.data
regd	byte 'EAX=',8 dup (0),'H',0	;显示EAX内容，预留8个字符（字节）空间
	.code
start:
	mov eax, 1234abcdh	;假设一个要显示的数据
	xor ebx,ebx	;使用EBX相对寻址访问REGD字符串
	mov ecx,8	;8位十六进制数
again:	rol eax,4	;高4位循环移位进入低4位，作为子程序的入口参数
	push eax	;子程序利用AL返回结果，所以需要保存EAX中的数据
	call htoasc	;调用子程序
	mov regd+4[ebx],al	;保存转换后的ASCII码
	pop eax	;恢复保存的数据
	inc ebx
	loop again
	mov eax,offset regd
	call dispmsg	;显示

	exit 0
htoasc	proc	;将AL低4位表达的一位十六进制数转换为ASCII码
	and al,0fh	;只取AL的低4位
	or al,30h	;AL高4位变成3，实现加30H
	cmp al,39h	;是0～9，还是A～F
	jbe htoend
	add al,7	;是A～F，其ASCII码再加上7
htoend:	ret	;子程序返回
htoasc	endp 

	end start
```

```
D:\MASM>make32 eg0503
Microsoft (R) Macro Assembler Version 6.15.8803
Copyright (C) Microsoft Corp 1981-2000.  All rights reserved.

 Assembling: eg0503.asm
Microsoft (R) Incremental Linker Version 6.00.8168
Copyright (C) Microsoft Corp 1992-1998. All rights reserved.

 驱动器 D 中的卷是 软件
 卷的序列号是 A4FA-4B01

 D:\MASM 的目录

2011/02/11  10:13               810 eg0503.asm
2023/10/25  16:16            32,803 eg0503.exe
2023/10/25  16:16            20,520 eg0503.ilk
2023/10/25  16:16             5,656 eg0503.lst
2023/10/25  16:16             2,249 eg0503.obj
2023/10/25  16:16            74,752 eg0503.pdb
               6 个文件        136,790 字节
               0 个目录 43,998,162,944 可用字节

D:\MASM>eg0503.exe
EAX=1234ABCDH
```


```asm
; eg0503a.asm in Windows Console
	include io32.inc
	.data
regd	byte 'EAX=',8 dup (0),'H',0	;显示EAX内容，预留8个字符（字节）空间
	.code
start:
	mov eax, 1234abcdh	;假设一个要显示的数据
	xor ebx,ebx	;使用EBX相对寻址访问REGD字符串
	mov ecx,8	;8位十六进制数
again:	rol eax,4	;高4位循环移位进入低4位，作为子程序的入口参数
	push eax	;子程序利用AL返回结果，所以需要保存EAX中的数据
	call htoasc	;调用子程序
	mov regd+4[ebx],al	;保存转换后的ASCII码
	pop eax	;恢复保存的数据
	inc ebx
	loop again
	mov eax,offset regd
	call dispmsg	;显示

	exit 0
htoasc	proc 
	and eax,0fh	;取AL低4位
	mov al,ASCII[eax] 	;换码
	ret
	;子程序的局部数据
ASCII	byte '0123456789ABCDEF'
htoasc	endp

	end start
```

```
D:\MASM>make32 eg0503a
Microsoft (R) Macro Assembler Version 6.15.8803
Copyright (C) Microsoft Corp 1981-2000.  All rights reserved.

 Assembling: eg0503a.asm
Microsoft (R) Incremental Linker Version 6.00.8168
Copyright (C) Microsoft Corp 1992-1998. All rights reserved.

 驱动器 D 中的卷是 软件
 卷的序列号是 A4FA-4B01

 D:\MASM 的目录

2011/02/11  10:13               705 eg0503a.asm
2023/10/25  16:41            32,804 eg0503a.exe
2023/10/25  16:41            20,524 eg0503a.ilk
2023/10/25  16:41             5,587 eg0503a.lst
2023/10/25  16:41             2,256 eg0503a.obj
2023/10/25  16:41            74,752 eg0503a.pdb
               6 个文件        136,628 字节
               0 个目录 43,998,007,296 可用字节

D:\MASM>eg0503a.exe
EAX=1234ABCDH
```


```asm
; eg0504.asm in Windows Console
	include io32.inc
	.data
array	dword 1234567890,-1234,0,1,-987654321,32767,-32768,5678,-5678,9000
writebuf	byte 12 dup(0)	;显示缓冲区

	.code
start:
	mov ecx,lengthof array
	mov ebx,0
again:	mov eax,array[ebx*4] 	;EAX＝入口参数
	call write	;调用子程序，显示一个数据
	call dispcrlf	;光标回车换行以便显示下一个数据
	inc ebx
	loop again

	exit 0
write	proc	;显示有符号十进制数的子程序，EAX＝入口参数
	push ebx	;保护寄存器
	push ecx
	push edx
	mov ebx,offset writebuf	;EBX指向显示缓冲区
	test eax,eax	;判断数据是零、正数或负数
	jnz write1	;不是零，跳转
	mov byte ptr [ebx],'0'	;是零，设置“0”
	inc ebx
	jmp write5	;转向显示
write1:	jns write2	;是正数，跳转
	mov byte ptr [ebx],'-'	;是负数，设置负号“－”
	inc ebx
	neg eax	;数据求补（绝对值）
write2:	mov ecx,10
	push ecx	;10压入堆栈，作为退出标志
write3:	cmp eax,0	;数据（商）为零，转向保存
	jz write4 
	xor edx,edx	;零位扩展被除数为EDX.EAX
	div ecx	;数据除以10：EDX.EAX÷10
	add edx,30h	;余数（0～9）转换为ASCII码
	push edx	;数据各位先低位后高位压入堆栈
	jmp write3
write4:	pop edx	;数据各位先高位后低位弹出堆栈
	cmp edx,ecx	;是结束标志10，转向显示
	je write5
	mov [ebx],dl	;数据保存到缓冲区
	inc ebx
	jmp write4
write5:	mov byte ptr [ebx],0	;给显示内容加上结尾标志
	mov eax,offset writebuf
	call dispmsg
	pop edx	;恢复寄存器
	pop ecx
	pop ebx
	ret	;子程序返回
write	endp

	end start
```

```
D:\MASM>make32 eg0504
Microsoft (R) Macro Assembler Version 6.15.8803
Copyright (C) Microsoft Corp 1981-2000.  All rights reserved.

 Assembling: eg0504.asm
Microsoft (R) Incremental Linker Version 6.00.8168
Copyright (C) Microsoft Corp 1992-1998. All rights reserved.

 驱动器 D 中的卷是 软件
 卷的序列号是 A4FA-4B01

 D:\MASM 的目录

2011/02/11  10:14             1,419 eg0504.asm
2023/10/25  16:48            32,803 eg0504.exe
2023/10/25  16:48            20,512 eg0504.ilk
2023/10/25  16:48             7,131 eg0504.lst
2023/10/25  16:48             2,775 eg0504.obj
2023/10/25  16:48            74,752 eg0504.pdb
               6 个文件        139,392 字节
               0 个目录 43,997,855,744 可用字节

D:\MASM>eg0504.exe
1234567890
-1234
0
1
-987654321
32767
-32768
5678
-5678
9000
```


## 共享变量传递参数

![](https://cdn.jsdelivr.net/gh/Rosefinch-Midsummer/MyImagesHost02/img/202310251046073.png)

![](https://cdn.jsdelivr.net/gh/Rosefinch-Midsummer/MyImagesHost02/img/202310251059287.png)


![](https://cdn.jsdelivr.net/gh/Rosefinch-Midsummer/MyImagesHost02/img/202310251059694.png)


![](https://cdn.jsdelivr.net/gh/Rosefinch-Midsummer/MyImagesHost02/img/202310251100906.png)

![](https://cdn.jsdelivr.net/gh/Rosefinch-Midsummer/MyImagesHost02/img/202310251101865.png)

![](https://cdn.jsdelivr.net/gh/Rosefinch-Midsummer/MyImagesHost02/img/202310251104694.png)

![](https://cdn.jsdelivr.net/gh/Rosefinch-Midsummer/MyImagesHost02/img/202310251104167.png)

![](https://cdn.jsdelivr.net/gh/Rosefinch-Midsummer/MyImagesHost02/img/202310251105092.png)


![](https://cdn.jsdelivr.net/gh/Rosefinch-Midsummer/MyImagesHost02/img/202310251105512.png)


temp是定义在数据段的变量，主程序将数据保存在temp变量中，子程序从该变量中取用数据，这是共享变量参数传递方式。

这里的共享变量对应C/C++语言的全局变量。

对于大量保存在主存储器缓冲器的参数，通常只能通过传递地址的方式，也被称为“传递引用”。

输入二进制数的正确数字应该只有0和1。

```asm
; eg0505.asm in Windows Console
	include io32.inc
	.data
count	= 5
array	dword count dup(0)
temp	dword ?	;共享变量

	.code
start:
	mov ecx,count
	mov ebx,offset array
again:	call rdbd	;调用子程序，输入一个数据
	mov eax,temp	;获得出口参数
	mov [ebx],eax	;存放到数据缓冲区
	add ebx,4
	loop again

	exit 0
rdbd	proc	;二进制输入子程序
	push eax	;出口参数：共享变量TEMP
	push ebx
 	push ecx
rdbd1:	xor ebx,ebx	;EBX用于存放二进制结果
	mov ecx,32	;限制输入字符的个数
rdbd2: 
	call readc	;输入一个字符
	cmp al,'0'	;检测键入字符是否合法
	jb rderr	;不合法则返回重新输入
	cmp al,'1'
	ja rderr
	sub al,'0'	;对输入的字符进行转化
	shl ebx,1	;EBX的值乘以2
	or bl,al	;BL和AL相加
	loop rdbd2	;循环键入字符
	mov temp,ebx	;把EBX的二进制结果存放TEMP返回
    call dispcrlf	;分行
	pop ecx
	pop ebx
	pop eax
	ret
rderr:	mov eax,offset errmsg	;显示错误信息
	call dispmsg
	jmp rdbd1
errmsg	byte 0dh,0ah,'Input error, enter again: ',0
rdbd	endp

	end start
```

```
D:\MASM>make32 eg0505
Microsoft (R) Macro Assembler Version 6.15.8803
Copyright (C) Microsoft Corp 1981-2000.  All rights reserved.

 Assembling: eg0505.asm
Microsoft (R) Incremental Linker Version 6.00.8168
Copyright (C) Microsoft Corp 1992-1998. All rights reserved.

 驱动器 D 中的卷是 软件
 卷的序列号是 A4FA-4B01

 D:\MASM 的目录

2011/02/11  10:41               976 eg0505.asm
2023/10/25  16:50            32,803 eg0505.exe
2023/10/25  16:50            20,512 eg0505.ilk
2023/10/25  16:50             6,497 eg0505.lst
2023/10/25  16:50             2,643 eg0505.obj
2023/10/25  16:50            74,752 eg0505.pdb
               6 个文件        138,183 字节
               0 个目录 43,997,704,192 可用字节

D:\MASM>eg0505.exe
12
Input error, enter again: 1111
Input error, enter again: 11
Input error, enter again: 1
Input error, enter again: 11111111111111111111111111111111

Input error, enter again: 11111
Input error, enter again: 11111111111111111111111111111111
1
Input error, enter again:
Input error, enter again: 11111111111111111111111111111111

Input error, enter again: 11111111111111111111111111111111

Input error, enter again: 11111111111111111111111111111111

D:\MASM>
```

输入5个32位二进制数后程序结束。

```asm
; eg0506.asm in Windows Console
	include io32.inc
	.data
count	= 5
array	dword count dup(0)
temp	dword ?	;共享变量
readbuf	byte 30 dup(0)

	.code
start:
	mov ecx,count
	mov ebx,offset array
again:	call read	;调用子程序，输入一个数据
	mov eax,temp	;获得出口参数
	mov [ebx],eax	;存放到数据缓冲区
	add ebx,4
	dec ecx
	jnz again

	exit 0
read	proc	;输入有符号十进制数的子程序
	push eax	;出口参数：变量TEMP＝补码表示的二进制数值
	push ebx	;说明：负数用“－”引导
	push ecx
	push edx
read0:	mov eax,offset readbuf
	call readmsg	;输入一个字符串
	test eax,eax
	jz readerr	;没有输入数据，转向错误处理
	cmp eax,12
	ja readerr	;输入超过12个字符，转向错误处理
	mov edx,offset readbuf	;EDX指向输入缓冲区
	xor ebx,ebx	;EBX保存结果
	xor ecx,ecx	;ECX为正负标志，0为正，－1为负
	mov al,[edx]	;取一个字符
	cmp al,'+'	;是“＋”，继续
	jz read1
	cmp al,'-'	;是“－”，设置－1标志
	jnz read2
	mov ecx,-1
read1:	inc edx	;取下一个字符
	mov al,[edx]
	test al,al	;是结尾0，转向求补码
	jz read3
read2:	cmp al,'0'	;不是0～9之间的数码，则输入错误
	jb readerr
	cmp al,'9'
	ja readerr
	sub al,30h	;是0～9之间的数码，则转换为二进制数
	imul ebx,10	;原数值乘10：EBX＝EBX×10
	jc readerr	;CF＝1，说明乘积溢出，输入数据超出32位范围，出错
	movzx eax,al	;零位扩展，便于相加
	add ebx,eax	;原数值乘10后，与新数码相加
	cmp ebx,80000000h	;数据超过231，出错
	jbe read1	;继续转换下一个数位
readerr:	mov eax,offset errmsg	;显示出错信息
	call dispmsg
	jmp read0
	;
read3:	test ecx,ecx	;判断是正数还是负数
	jz read4
	neg ebx	;是负数，进行求补
	jmp read5
read4:	cmp ebx,7fffffffh	;正数超过231-1，出错
	ja readerr
read5:	mov temp,ebx	;设置出口参数
	pop edx
	pop ecx
	pop ebx
	pop eax
	ret	;子程序返回
errmsg	byte 'Input error, enter again: ',0
read	endp

	end start
```

```
D:\MASM>make32 eg0506
Microsoft (R) Macro Assembler Version 6.15.8803
Copyright (C) Microsoft Corp 1981-2000.  All rights reserved.

 Assembling: eg0506.asm
Microsoft (R) Incremental Linker Version 6.00.8168
Copyright (C) Microsoft Corp 1992-1998. All rights reserved.

 驱动器 D 中的卷是 软件
 卷的序列号是 A4FA-4B01

 D:\MASM 的目录

2011/02/11  10:43             1,791 eg0506.asm
2023/10/25  17:30            32,803 eg0506.exe
2023/10/25  17:30            20,512 eg0506.ilk
2023/10/25  17:30             8,135 eg0506.lst
2023/10/25  17:30             3,187 eg0506.obj
2023/10/25  17:30            74,752 eg0506.pdb
               6 个文件        141,180 字节
               0 个目录 43,997,503,488 可用字节

D:\MASM>eg0506.exe
-20
30
10
20
30

D:\MASM>
```
## 堆栈传递参数


![](https://cdn.jsdelivr.net/gh/Rosefinch-Midsummer/MyImagesHost02/img/202310251453138.png)


![](https://cdn.jsdelivr.net/gh/Rosefinch-Midsummer/MyImagesHost02/img/202310251454104.png)

![](https://cdn.jsdelivr.net/gh/Rosefinch-Midsummer/MyImagesHost02/img/202310251457081.png)

![](https://cdn.jsdelivr.net/gh/Rosefinch-Midsummer/MyImagesHost02/img/202310251505909.png)

![](https://cdn.jsdelivr.net/gh/Rosefinch-Midsummer/MyImagesHost02/img/202310251505031.png)

![](https://cdn.jsdelivr.net/gh/Rosefinch-Midsummer/MyImagesHost02/img/202310251506613.png)





![](https://cdn.jsdelivr.net/gh/Rosefinch-Midsummer/MyImagesHost02/img/202310251503231.png)

![](https://cdn.jsdelivr.net/gh/Rosefinch-Midsummer/MyImagesHost02/img/202310251504200.png)



![](https://cdn.jsdelivr.net/gh/Rosefinch-Midsummer/MyImagesHost02/img/202310251511164.png)

主程序压入堆栈8个字节数据传递给子程序，平衡堆栈时需要将ESP加8

除法指令“idiv ecx”的64位被除数在：EDX和EAX中

在80x86处理器上，C语言进行函数调用带入的参数实质上就是采用堆栈传递参数。

如果删除主程序指令“add esp,8”，但子程序中“ret”指令改为“ret 8”，示例程序仍能正确完成预期功能。

IA-32处理器中，利用EBP（或ESP）、而不是EBX、ESI等间接或相对寻址访问堆栈，是因为EBP（或ESP）间接或相对寻址的默认逻辑段是堆栈段。

```asm
; eg0507.asm in Windows Console
	include io32.inc
	.data
array	dword 675, 354, -34, 198, 267, 0, 9, 2371, -67, 4257

	.code
start:
	push lengthof array	;压入数据个数
	push offset array	;压数组的偏移地址
	call mean	;调用求平均值子程序，出口参数：EAX＝平均值（整数部分）
	add esp,8	;平衡堆栈（压入了8个字节数据）
	call dispsid	;显示

	exit 0
mean	proc	;计算32位有符号数平均值子程序
	push ebp	;入口参数：顺序压入数据个数和数组偏移地址
	mov ebp,esp	;出口参数：EAX＝平均值
	push ebx	;保护寄存器
	push ecx
	push edx
	mov ebx,[ebp+8]	;EBX＝堆栈中取出的偏移地址
	mov ecx,[ebp+12]	;ECX＝堆栈中取出的数据个数
	xor eax,eax	;EAX保存和值
	xor edx,edx	;EDX＝指向数组元素
mean1:	add eax,[ebx+edx*4]	;求和
	add edx,1	;指向下一个数据
	cmp edx,ecx	;比较个数
	jb mean1	;循环
	cdq	;将累加和EAX符号扩展到EDX
	idiv ecx	;有符号数除法，EAX＝平均值（余数在EDX中）
	pop edx	;恢复寄存器
	pop ecx
	pop ebx
	pop ebp
	ret
mean	endp

	end start
```

```
D:\MASM>make32 eg0507
Microsoft (R) Macro Assembler Version 6.15.8803
Copyright (C) Microsoft Corp 1981-2000.  All rights reserved.

 Assembling: eg0507.asm
Microsoft (R) Incremental Linker Version 6.00.8168
Copyright (C) Microsoft Corp 1992-1998. All rights reserved.

 驱动器 D 中的卷是 软件
 卷的序列号是 A4FA-4B01

 D:\MASM 的目录

2011/02/11  10:45               950 eg0507.asm
2023/10/25  17:31            32,803 eg0507.exe
2023/10/25  17:31            20,512 eg0507.ilk
2023/10/25  17:31             5,870 eg0507.lst
2023/10/25  17:31             2,205 eg0507.obj
2023/10/25  17:31            74,752 eg0507.pdb
               6 个文件        137,092 字节
               0 个目录 43,997,351,936 可用字节

D:\MASM>eg0507.exe
803
```

```asm
; eg0507a.asm in Windows Console
	include io32.inc
	.data
array	dword 675, 354, -34, 198, 267, 0, 9, 2371, -67, 4257

	.code
start:
	push lengthof array	;压入数据个数
	push offset array	;压数组的偏移地址
	call mean	;调用求平均值子程序，出口参数：EAX＝平均值（整数部分）
	add esp,8	;平衡堆栈（压入了8个字节数据）
	call dispsid	;显示

	exit 0
mean	proc	;计算32位有符号数平均值子程序
	push ebp	;入口参数：顺序压入数据个数和数据缓冲区偏移地址
	mov ebp,esp	;出口参数：EAX＝平均值
	push ebx	;保护寄存器
	push ecx
	push edx
	push esi
	push edi
	mov ebx,[ebp+8]	;EBX＝堆栈中取出的偏移地址
	mov ecx,[ebp+12]	;ECX＝堆栈中取出的数据个数
	xor esi,esi	;ESI＝求和的低32位值
	mov edi,esi	;EDI＝求和的高32位值
mean1:	mov eax,[ebx]	;EAX＝取出一个数据
	cdq	;EAX符号扩展到EDX
	add esi,eax	;求和低32位
	adc edi,edx	;求和高32位
	add ebx,4	;指向下一个数据
	dec ecx	;数据个数减少一个
	jnz mean1	;循环（这两条指令等同于LOOP指令）
	mov eax,esi	;累加和在EDX.EAX
	mov edx,edi
	idiv dword ptr [ebp+12]	;有符号数除法，EAX＝平均值（余数在EDX中）
	pop edi	;恢复寄存器
	pop esi
	pop edx
	pop ecx
	pop ebx
	pop ebp
	ret
mean	endp

	end start
```

```asm
; eg0507b.asm in Windows Console
	include io32.inc
	.data
array	dword 675, 354, -34, 198, 267, 0, 9, 2371, -67, 4257

	.code
start:
	push lengthof array	;压入数据个数
	push offset array	;压数组的偏移地址
	call mean	;调用求平均值子程序，出口参数：EAX＝平均值（整数部分）
	add esp,8	;平衡堆栈（压入了8个字节数据）
	call dispsid	;显示

	exit 0
mean	proc	;计算32位有符号数平均值子程序
	push ebp	;入口参数：顺序压入数据个数和数据缓冲区偏移地址
	mov ebp,esp	;出口参数：EAX＝平均值
	push ebx	;保护寄存器
	push ecx
	push edx
	push esi
	push edi
	mov ebx,[ebp+8]	;EBX＝堆栈中取出的偏移地址
	mov ecx,[ebp+12]	;ECX＝堆栈中取出的数据个数
    cmp ecx,0
    jnz mean0
    xor eax,eax
    jmp mean2
mean0:	xor esi,esi	;ESI＝求和的低32位值
	mov edi,esi	;EDI＝求和的高32位值
mean1:	mov eax,[ebx]	;EAX＝取出一个数据
	cdq	;EAX符号扩展到EDX
	add esi,eax	;求和低32位
	adc edi,edx	;求和高32位
	add ebx,4	;指向下一个数据
	dec ecx	;数据个数减少一个
	jnz mean1	;循环（这两条指令等同于LOOP指令）
	mov eax,esi	;累加和在EDX.EAX
	mov edx,edi
	idiv dword ptr [ebp+12]	;有符号数除法，EAX＝平均值（余数在EDX中）
mean2:	pop edi	;恢复寄存器
	pop esi
	pop edx
	pop ecx
	pop ebx
	pop ebp
	ret
mean	endp

	end start
```




